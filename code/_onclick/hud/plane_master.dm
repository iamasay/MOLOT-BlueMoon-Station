/*

	† † † † † † † † † † † † † † † † † † † † † † † † † † † † † †

	Отче наш, сущий на небесах!

	Да святится имя Твое;

	Да приидет Царствие Твое;

	да будет воля Твоя и на земле, как на небе;

	Хлеб наш насущный дай нам на сей день;

	И прости нам долги наши, как и мы прощаем должникам нашим;

	И не введи нас в искушение, но избавь нас от лукавого.

	Ибо Твое есть Царство и сила и слава вовеки. Аминь.

													Мф. 6:9-13.

	† † † † † † † † † † † † † † † † † † † † † † † † † † † † † †

*/

/atom/movable/screen/plane_master
	screen_loc = "CENTER"
	icon_state = "blank"
	appearance_flags = PLANE_MASTER|NO_CLIENT_COLOR
	blend_mode = BLEND_OVERLAY
	var/show_alpha = 255
	var/hide_alpha = 0

/atom/movable/screen/plane_master/proc/Show(override)
	alpha = override || show_alpha

/atom/movable/screen/plane_master/proc/Hide(override)
	alpha = override || hide_alpha

//Why do plane masters need a backdrop sometimes? Read https://secure.byond.com/forum/?post=2141928
//Trust me, you need one. Period. If you don't think you do, you're doing something extremely wrong.
/atom/movable/screen/plane_master/proc/backdrop(mob/mymob)

/atom/movable/screen/plane_master/Destroy()
	for(var/filter_name in list("singularity_0", "singularity_1", "singularity_2", "singularity_3"))
		var/filter = get_filter(filter_name)
		if(filter)
			animate(filter)
	return ..()

///Things rendered on "openspace"; holes in multi-z
/atom/movable/screen/plane_master/openspace
	name = "open space plane master"
	plane = OPENSPACE_BACKDROP_PLANE
	appearance_flags = PLANE_MASTER
	blend_mode = BLEND_MULTIPLY
	alpha = 255

/atom/movable/screen/plane_master/openspace/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	add_filter("displacer", 1, displacement_map_filter(render_source = GRAVITY_PULSE_RENDER_TARGET, size = 10))

	add_filter("singularity_0", 1, displacement_map_filter(render_source = SINGULARITY_0_RENDER_TARGET, size = -20))
	add_filter("singularity_1", 2, displacement_map_filter(render_source = SINGULARITY_1_RENDER_TARGET, size = 75))
	add_filter("singularity_2", 3, displacement_map_filter(render_source = SINGULARITY_2_RENDER_TARGET, size = 400))
	add_filter("singularity_3", 4, displacement_map_filter(render_source = SINGULARITY_3_RENDER_TARGET, size = 700))

	animate(get_filter("singularity_0"), size = -20, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = -30, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_1"), size = 50, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 100, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_2"), size = 400, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 300, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_3"), size = 750, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 600, time = 10, easing = LINEAR_EASING, loop = -1)

	filters += filter(type="alpha", render_source=FIELD_OF_VISION_RENDER_TARGET, flags=MASK_INVERSE)
	filters += filter(type="alpha", render_source = LIGHTING_RENDER_TARGET, flags = MASK_INVERSE)
	filters += filter(type = "drop_shadow", color = "#04080FAA", size = -10)
	filters += filter(type = "drop_shadow", color = "#04080FAA", size = -15)
	filters += filter(type = "drop_shadow", color = "#04080FAA", size = -20)

/atom/movable/screen/plane_master/proc/outline(_size, _color)
	filters += filter(type = "outline", size = _size, color = _color)

/atom/movable/screen/plane_master/proc/shadow(_size, _offset = 0, _x = 0, _y = 0, _color = "#04080FAA")
	filters += filter(type = "drop_shadow", x = _x, y = _y, color = _color, size = _size, offset = _offset)

///Contains just the floor
/atom/movable/screen/plane_master/floor
	name = "floor plane master"
	plane = FLOOR_PLANE
	appearance_flags = PLANE_MASTER
	blend_mode = BLEND_OVERLAY

/atom/movable/screen/plane_master/floor/backdrop(mob/mymob)
	if(mymob?.client?.prefs?.ambientocclusion)
		var/blur_lvl = mymob?.client?.prefs?.lighting_blur || 0
		add_filter("ambient_occlusion", 0, AMBIENT_OCCLUSION_SCALED(2, "#04080F32", blur_lvl))
	else
		remove_filter("ambient_occlusion")

/atom/movable/screen/plane_master/floor/Initialize(mapload)
	. = ..()
	add_filter("displacer", 1, displacement_map_filter(render_source = GRAVITY_PULSE_RENDER_TARGET, size = 10))

	add_filter("singularity_0", 1, displacement_map_filter(render_source = SINGULARITY_0_RENDER_TARGET, size = -20))
	add_filter("singularity_1", 2, displacement_map_filter(render_source = SINGULARITY_1_RENDER_TARGET, size = 75))
	add_filter("singularity_2", 3, displacement_map_filter(render_source = SINGULARITY_2_RENDER_TARGET, size = 400))
	add_filter("singularity_3", 4, displacement_map_filter(render_source = SINGULARITY_3_RENDER_TARGET, size = 700))

	animate(get_filter("singularity_0"), size = -20, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = -30, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_1"), size = 50, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 100, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_2"), size = 400, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 300, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_3"), size = 750, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 600, time = 10, easing = LINEAR_EASING, loop = -1)

/atom/movable/screen/plane_master/wall
	name = "wall plane master"
	plane = WALL_PLANE
	appearance_flags = PLANE_MASTER

/atom/movable/screen/plane_master/wall/backdrop(mob/mymob)
	if(mymob?.client?.prefs?.ambientocclusion)
		var/blur_lvl = mymob?.client?.prefs?.lighting_blur || 0
		add_filter("ambient_occlusion", 0, AMBIENT_OCCLUSION_SCALED(4, "#04080FAA", blur_lvl))
	else
		remove_filter("ambient_occlusion")

/atom/movable/screen/plane_master/above_wall
	name = "above wall plane master"
	plane = ABOVE_WALL_PLANE
	appearance_flags = PLANE_MASTER

/atom/movable/screen/plane_master/above_wall/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	add_filter("displacer", 1, displacement_map_filter(render_source = GRAVITY_PULSE_RENDER_TARGET, size = 10))

	add_filter("singularity_0", 1, displacement_map_filter(render_source = SINGULARITY_0_RENDER_TARGET, size = -20))
	add_filter("singularity_1", 2, displacement_map_filter(render_source = SINGULARITY_1_RENDER_TARGET, size = 75))
	add_filter("singularity_2", 3, displacement_map_filter(render_source = SINGULARITY_2_RENDER_TARGET, size = 400))
	add_filter("singularity_3", 4, displacement_map_filter(render_source = SINGULARITY_3_RENDER_TARGET, size = 700))

	animate(get_filter("singularity_0"), size = -20, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = -30, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_1"), size = 50, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 100, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_2"), size = 400, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 300, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_3"), size = 750, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 600, time = 10, easing = LINEAR_EASING, loop = -1)

	add_filter("vision_cone", 100, list(type="alpha", render_source=FIELD_OF_VISION_RENDER_TARGET, flags=MASK_INVERSE))

/atom/movable/screen/plane_master/above_wall/backdrop(mob/mymob)
	if(mymob?.client?.prefs?.ambientocclusion)
		var/blur_lvl = mymob?.client?.prefs?.lighting_blur || 0
		add_filter("ambient_occlusion", 0, AMBIENT_OCCLUSION_SCALED(3, "#04080F64", blur_lvl))
	else
		remove_filter("ambient_occlusion")

///Contains most things in the game world
/atom/movable/screen/plane_master/game_world
	name = "game world plane master"
	plane = GAME_PLANE
	appearance_flags = PLANE_MASTER //should use client color
	blend_mode = BLEND_OVERLAY
	render_target = GAME_PLANE_RENDER_TARGET

/atom/movable/screen/plane_master/game_world/Initialize(mapload, datum/hud/hud_owner)
	. = ..()

	add_filter("displacer", 1, displacement_map_filter(render_source = GRAVITY_PULSE_RENDER_TARGET, size = 10))

	add_filter("singularity_0", 1, displacement_map_filter(render_source = SINGULARITY_0_RENDER_TARGET, size = -20))
	add_filter("singularity_1", 2, displacement_map_filter(render_source = SINGULARITY_1_RENDER_TARGET, size = 75))
	add_filter("singularity_2", 3, displacement_map_filter(render_source = SINGULARITY_2_RENDER_TARGET, size = 400))
	add_filter("singularity_3", 4, displacement_map_filter(render_source = SINGULARITY_3_RENDER_TARGET, size = 700))

	animate(get_filter("singularity_0"), size = -20, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = -30, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_1"), size = 50, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 100, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_2"), size = 400, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 300, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_3"), size = 750, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 600, time = 10, easing = LINEAR_EASING, loop = -1)

	add_filter("vision_cone", 100, list(type="alpha", render_source=FIELD_OF_VISION_RENDER_TARGET, flags=MASK_INVERSE))

/atom/movable/screen/plane_master/game_world/backdrop(mob/mymob)
	if(mymob?.client?.prefs?.ambientocclusion)
		var/blur_lvl = mymob?.client?.prefs?.lighting_blur || 0
		add_filter("ambient_occlusion", 0, AMBIENT_OCCLUSION_SCALED(4, "#04080FAA", blur_lvl))
	else
		remove_filter("ambient_occlusion")

///Contains all shadow cone masks, whose image overrides are displayed only to their respective owners.
/atom/movable/screen/plane_master/field_of_vision
	name = "field of vision mask plane master"
	plane = FIELD_OF_VISION_PLANE
	render_target = FIELD_OF_VISION_RENDER_TARGET
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT

/atom/movable/screen/plane_master/field_of_vision/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	filters += filter(type="alpha", render_source=FIELD_OF_VISION_BLOCKER_RENDER_TARGET, flags=MASK_INVERSE)

///Used to display the owner and its adjacent surroundings through the FoV plane mask.
/atom/movable/screen/plane_master/field_of_vision_blocker
	name = "field of vision blocker plane master"
	plane = FIELD_OF_VISION_BLOCKER_PLANE
	render_target = FIELD_OF_VISION_BLOCKER_RENDER_TARGET
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT

///Stores the visible portion of the FoV shadow cone.
/atom/movable/screen/plane_master/field_of_vision_visual
	name = "field of vision visual plane master"
	plane = FIELD_OF_VISION_VISUAL_PLANE
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT

/atom/movable/screen/plane_master/field_of_vision_visual/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	filters += filter(type="alpha", render_source=FIELD_OF_VISION_BLOCKER_RENDER_TARGET, flags=MASK_INVERSE)

///Contains all lighting objects
/atom/movable/screen/plane_master/lighting
	name = "lighting plane master"
	plane = LIGHTING_PLANE
	blend_mode = BLEND_MULTIPLY
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT

/atom/movable/screen/plane_master/lighting/backdrop(mob/mymob)
	if(!mymob)
		return
	mymob.overlay_fullscreen("lighting_backdrop_lit", /atom/movable/screen/fullscreen/special/lighting_backdrop/lit)
	mymob.overlay_fullscreen("lighting_backdrop_unlit", /atom/movable/screen/fullscreen/special/lighting_backdrop/unlit)
	var/is_fast = (mymob?.client?.prefs?.lighting_quality == LIGHTING_QUALITY_FAST)
	if(is_fast)
		remove_filter("lighting_blur")
		remove_filter("lighting_blur_edge_fix")
	else
		var/blur_level = mymob?.client?.prefs?.lighting_blur || 0
		var/effective_blur = LIGHTING_BLUR_BASE + blur_level * LIGHTING_BLUR_MULTIPLIER
		if(effective_blur > 0)
			add_filter("lighting_blur", 0, list("type" = "blur", "size" = effective_blur))
			// Force alpha=1 after blur to prevent edge bleeding - blur samples transparent pixels
			// outside the render target boundary, creating semi-transparent edges that weaken
			// BLEND_MULTIPLY darkening and produce false light strips at screen edges
			add_filter("lighting_blur_edge_fix", 1, color_matrix_filter(list(1,0,0,0, 0,1,0,0, 0,0,1,0, 0,0,0,0, 0,0,0,1)))
		else
			remove_filter("lighting_blur")
			remove_filter("lighting_blur_edge_fix")
	remove_filter("user_brightness")
	if(is_fast)
		// В режиме Быстро - стандартное освещение до LightUp, без пользовательской яркости
		return
	var/brightness = mymob?.client?.prefs?.lighting_brightness
	if(isnull(brightness))
		brightness = LIGHTING_BRIGHTNESS_DEFAULT
	brightness = clamp(brightness, LIGHTING_BRIGHTNESS_MIN, LIGHTING_BRIGHTNESS_MAX)
	// Диапазон 0-60 растянут на 0-100: 0 = старая 0, 100 = старая 60
	var/mapped = brightness * 0.6
	var/ratio = (mapped - LIGHTING_BRIGHTNESS_DEFAULT) / 200
	if(ratio != 0)
		add_filter("user_brightness", 5, color_matrix_filter(list(
			1,0,0,0,
			0,1,0,0,
			0,0,1,0,
			0,0,0,1,
			ratio, ratio, ratio, 0
		)))

/*!
 * This system works by exploiting BYONDs color matrix filter to use layers to handle emissive blockers.
 *
 * Emissive overlays are pasted with an atom color that converts them to be entirely some specific color.
 * Emissive blockers are pasted with an atom color that converts them to be entirely some different color.
 * Emissive overlays and emissive blockers are put onto the same plane.
 * The layers for the emissive overlays and emissive blockers cause them to mask eachother similar to normal BYOND objects.
 * A color matrix filter is applied to the emissive plane to mask out anything that isn't whatever the emissive color is.
 * This is then used to alpha mask the lighting plane.
 */

/atom/movable/screen/plane_master/lighting/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	add_filter("emissives", 2, alpha_mask_filter(render_source = EMISSIVE_RENDER_TARGET, flags = MASK_INVERSE))
	apply_light_cutoff(0)
	add_filter("object_lighting", 3, alpha_mask_filter(render_source = O_LIGHTING_VISUAL_RENDER_TARGET, flags = MASK_INVERSE))
	add_filter("displacer", 4, displacement_map_filter(render_source = GRAVITY_PULSE_RENDER_TARGET, size = 10))

	add_filter("singularity_0", 2, displacement_map_filter(render_source = SINGULARITY_0_RENDER_TARGET, size = -20))
	add_filter("singularity_1", 3, displacement_map_filter(render_source = SINGULARITY_1_RENDER_TARGET, size = 75))
	add_filter("singularity_2", 3, displacement_map_filter(render_source = SINGULARITY_2_RENDER_TARGET, size = 400))
	add_filter("singularity_3", 4, displacement_map_filter(render_source = SINGULARITY_3_RENDER_TARGET, size = 700))

	animate(get_filter("singularity_0"), size = -20, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = -30, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_1"), size = 50, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 100, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_2"), size = 400, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 300, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_3"), size = 750, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 600, time = 10, easing = LINEAR_EASING, loop = -1)

/atom/movable/screen/plane_master/lighting/proc/apply_light_cutoff(cutoff, list/color_cutoffs)
	remove_filter("light_cutoff")
	if(!cutoff && !color_cutoffs)
		return
	var/ratio = cutoff / 100
	var/list/rgb_add = list(ratio, ratio, ratio)
	if(length(color_cutoffs) == 3)
		rgb_add[1] += color_cutoffs[1] / 100
		rgb_add[2] += color_cutoffs[2] / 100
		rgb_add[3] += color_cutoffs[3] / 100
	add_filter("light_cutoff", 6, color_matrix_filter(list(
		1,0,0,0,
		0,1,0,0,
		0,0,1,0,
		0,0,0,1,
		rgb_add[1], rgb_add[2], rgb_add[3], 0
	)))

///Оверлейный свет (/datum/component/overlay_lighting): BLEND_ADD-маски источников собираются здесь.
///Плоскость тонирует игру цветом света (BLEND_MULTIPLY), а её рендер-таргет прорезает тьму
///lighting plane через фильтр "object_lighting" (см. Initialize lighting plane master выше).
/atom/movable/screen/plane_master/o_light_visual
	name = "overlight light visual plane master"
	plane = O_LIGHTING_VISUAL_PLANE
	render_target = O_LIGHTING_VISUAL_RENDER_TARGET
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	blend_mode = BLEND_MULTIPLY

/**
 * Handles emissive overlays and emissive blockers.
 */
/atom/movable/screen/plane_master/emissive
	name = "emissive plane master"
	plane = EMISSIVE_PLANE
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	render_target = EMISSIVE_RENDER_TARGET

/atom/movable/screen/plane_master/emissive/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	add_filter("em_block_masking", 1, color_matrix_filter(GLOB.em_mask_matrix))
	// emissive_bloom added conditionally in backdrop() based on blur quality setting

/atom/movable/screen/plane_master/emissive/backdrop(mob/mymob)
	if(mymob?.client?.prefs?.lighting_quality == LIGHTING_QUALITY_FAST)
		remove_filter("emissive_bloom")
		return
	var/blur_level = mymob?.client?.prefs?.lighting_blur || 0
	var/bloom_intensity = mymob?.client?.prefs?.lighting_bloom_intensity
	if(isnull(bloom_intensity))
		bloom_intensity = LIGHTING_BLOOM_INTENSITY_DEFAULT
	if(bloom_intensity <= 0)
		remove_filter("emissive_bloom")
		return
	if(blur_level >= 2)
		// Bloom on emissive - intensity 0-200, 100 = старый максимум (текущие 100 -> 50)
		var/bloom_alpha = clamp(30 + bloom_intensity * 0.32, 20, 85)
		var/bloom_size = max(1, round(blur_level * (0.6 + bloom_intensity / 100 * 0.6)))
		add_filter("emissive_bloom", 2, bloom_filter(threshold = COLOR_BLACK, size = bloom_size, offset = 1, alpha = bloom_alpha))
	else
		remove_filter("emissive_bloom")

/atom/movable/screen/plane_master/lamps
	name = "lamps plane master"
	plane = LIGHTING_LAMPS_PLANE
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	render_target = LIGHTING_LAMPS_RENDER_TARGET

/atom/movable/screen/plane_master/lamps/backdrop(mob/mymob)
	remove_filter("user_brightness")
	if(mymob?.client?.prefs?.lighting_quality == LIGHTING_QUALITY_FAST)
		return
	var/brightness = mymob?.client?.prefs?.lighting_lamp_brightness
	if(isnull(brightness))
		brightness = LIGHTING_LAMP_BRIGHTNESS_DEFAULT
	brightness = clamp(brightness, LIGHTING_LAMP_BRIGHTNESS_MIN, LIGHTING_LAMP_BRIGHTNESS_MAX)
	// Диапазон ламп 60-100 растянут на 0-100, нормализовано: макс +0.25
	var/mapped = 60 + brightness * 0.4
	var/ratio = (mapped - LIGHTING_LAMP_BRIGHTNESS_DEFAULT) / 200
	if(ratio != 0)
		add_filter("user_brightness", 5, color_matrix_filter(list(
			1,0,0,0,
			0,1,0,0,
			0,0,1,0,
			0,0,0,1,
			ratio, ratio, ratio, 0
		)))

/atom/movable/screen/plane_master/lamps/floor
	name = "floor lamps plane master"
	plane = FLOOR_LIGHTING_LAMPS_PLANE
	render_target = FLOOR_LIGHTING_LAMPS_RENDER_TARGET

/atom/movable/screen/plane_master/lamps/floor/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	add_filter("floor_game_mask", 1, alpha_mask_filter(render_source = GAME_PLANE_RENDER_TARGET, flags = MASK_INVERSE))

/atom/movable/screen/plane_master/exposure
	name = "exposure plane master"
	plane = LIGHTING_EXPOSURE_PLANE
	appearance_flags = PLANE_MASTER|PIXEL_SCALE
	blend_mode = BLEND_ADD
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT

/atom/movable/screen/plane_master/exposure/backdrop(mob/mymob)
	remove_filter("blur_exposure")
	remove_filter("user_brightness")
	alpha = 0
	if(!istype(mymob) || !mymob.client)
		return
	if(mymob.client.prefs.lighting_quality == LIGHTING_QUALITY_FAST)
		return
	var/brightness = mymob?.client?.prefs?.lighting_lamp_brightness
	if(isnull(brightness))
		brightness = LIGHTING_LAMP_BRIGHTNESS_DEFAULT
	brightness = clamp(brightness, LIGHTING_LAMP_BRIGHTNESS_MIN, LIGHTING_LAMP_BRIGHTNESS_MAX)
	var/mapped = 60 + brightness * 0.4
	var/ratio = (mapped - LIGHTING_LAMP_BRIGHTNESS_DEFAULT) / 200
	if(ratio != 0)
		add_filter("user_brightness", 5, color_matrix_filter(list(
			1,0,0,0,
			0,1,0,0,
			0,0,1,0,
			0,0,0,1,
			ratio, ratio, ratio, 0
		)))
	var/has_legacy_light_pref = ("light" in mymob.client.prefs.vars)
	var/enabled = TRUE
	if(has_legacy_light_pref)
		enabled = (mymob.client.prefs.light & LIGHT_EXPOSURE)
	else
		enabled = (mymob.client.prefs.lighting_blur >= 1)
	if(enabled)
		alpha = 255
		var/bloom_intensity = mymob.client.prefs.lighting_bloom_intensity
		if(isnull(bloom_intensity))
			bloom_intensity = LIGHTING_BLOOM_INTENSITY_DEFAULT
		// Экспозиция масштабируется интенсивностью 0-200
		var/blur_size = clamp(12 + bloom_intensity * 0.12, 8, 28)
		add_filter("blur_exposure", 1, gauss_blur_filter(size = blur_size))

/atom/movable/screen/plane_master/lamps_selfglow
	name = "lamps selfglow plane master"
	plane = LIGHTING_LAMPS_SELFGLOW
	appearance_flags = PLANE_MASTER
	blend_mode = BLEND_ADD
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	var/target_rendering = LIGHTING_LAMPS_RENDER_TARGET

/atom/movable/screen/plane_master/lamps_selfglow/floor
	name = "floor lamps selfglow plane master"
	plane = FLOOR_LIGHTING_LAMPS_SELFGLOW
	target_rendering = FLOOR_LIGHTING_LAMPS_RENDER_TARGET

/atom/movable/screen/plane_master/lamps_selfglow/floor/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	add_filter("floor_selfglow_game_mask", 1, alpha_mask_filter(render_source = GAME_PLANE_RENDER_TARGET, flags = MASK_INVERSE))

/atom/movable/screen/plane_master/lamps_selfglow/backdrop(mob/mymob)
	remove_filter("add_lamps_to_selfglow")
	remove_filter("lamps_selfglow_bloom")
	remove_filter("user_brightness")
	if(!istype(mymob) || !mymob.client)
		return
	if(mymob.client.prefs.lighting_quality == LIGHTING_QUALITY_FAST)
		return
	var/brightness = mymob?.client?.prefs?.lighting_lamp_brightness
	if(isnull(brightness))
		brightness = LIGHTING_LAMP_BRIGHTNESS_DEFAULT
	brightness = clamp(brightness, LIGHTING_LAMP_BRIGHTNESS_MIN, LIGHTING_LAMP_BRIGHTNESS_MAX)
	var/mapped = 60 + brightness * 0.4
	var/ratio = (mapped - LIGHTING_LAMP_BRIGHTNESS_DEFAULT) / 200
	if(ratio != 0)
		add_filter("user_brightness", 5, color_matrix_filter(list(
			1,0,0,0,
			0,1,0,0,
			0,0,1,0,
			0,0,0,1,
			ratio, ratio, ratio, 0
		)))
	var/has_legacy_light_pref = ("light" in mymob.client.prefs.vars)
	var/has_glowlevel = ("glowlevel" in mymob.client.prefs.vars)
	var/level
	if(has_legacy_light_pref && has_glowlevel)
		if(!(mymob.client.prefs.light & LIGHT_NEW_LIGHTING))
			return
		level = mymob.client.prefs.glowlevel
	else
		var/blur = mymob.client.prefs.lighting_blur || 0
		if(blur <= 0)
			return
		else if(blur == 1)
			level = GLOW_LOW
		else if(blur == 2)
			level = GLOW_MED
		else
			level = GLOW_HIGH
	if(isnull(level))
		return
	var/bloomsize = 0
	var/bloomoffset = 0
	switch(level)
		if(GLOW_LOW)
			bloomsize = 2
			bloomoffset = 1
		if(GLOW_MED)
			bloomsize = 3
			bloomoffset = 2
		if(GLOW_HIGH)
			bloomsize = 4
			bloomoffset = 2
		else
			return
	var/bloom_intensity = mymob.client.prefs.lighting_bloom_intensity
	if(isnull(bloom_intensity))
		bloom_intensity = LIGHTING_BLOOM_INTENSITY_DEFAULT
	if(bloom_intensity <= 0)
		return
	// Блум 0-200, где 100 = старый максимум (текущие 100 -> 50)
	var/bloom_alpha = clamp(25 + bloom_intensity * 0.28, 15, 85)
	var/bloom_scale = 0.5 + bloom_intensity / 200 * 0.9
	bloomsize = max(1, round(bloomsize * bloom_scale))
	bloomoffset = max(1, round(bloomoffset * bloom_scale))
	add_filter("add_lamps_to_selfglow", 1, layering_filter(render_source = target_rendering, blend_mode = BLEND_OVERLAY))
	add_filter("lamps_selfglow_bloom", 1, bloom_filter(threshold = LIGHT_COLOR_BLOOM_THRESHOLD, size = bloomsize, offset = bloomoffset, alpha = bloom_alpha))

/atom/movable/screen/plane_master/lamps_glare
	name = "lamps glare plane master"
	plane = LIGHTING_LAMPS_GLARE
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	var/target_rendering = LIGHTING_LAMPS_RENDER_TARGET

/atom/movable/screen/plane_master/lamps_glare/floor
	name = "floor lamps glare plane master"
	plane = FLOOR_LIGHTING_LAMPS_GLARE
	target_rendering = FLOOR_LIGHTING_LAMPS_RENDER_TARGET

/atom/movable/screen/plane_master/lamps_glare/floor/Initialize(mapload, datum/hud/hud_owner)
	. = ..()
	add_filter("floor_glare_game_mask", 1, alpha_mask_filter(render_source = GAME_PLANE_RENDER_TARGET, flags = MASK_INVERSE))

/atom/movable/screen/plane_master/lamps_glare/backdrop(mob/mymob)
	remove_filter("add_lamps_to_glare")
	remove_filter("lamps_glare")
	remove_filter("user_brightness")
	if(!istype(mymob) || !mymob.client)
		return
	if(mymob.client.prefs.lighting_quality == LIGHTING_QUALITY_FAST)
		return
	var/brightness = mymob?.client?.prefs?.lighting_lamp_brightness
	if(isnull(brightness))
		brightness = LIGHTING_LAMP_BRIGHTNESS_DEFAULT
	brightness = clamp(brightness, LIGHTING_LAMP_BRIGHTNESS_MIN, LIGHTING_LAMP_BRIGHTNESS_MAX)
	var/mapped = 60 + brightness * 0.4
	var/ratio = (mapped - LIGHTING_LAMP_BRIGHTNESS_DEFAULT) / 200
	if(ratio != 0)
		add_filter("user_brightness", 5, color_matrix_filter(list(
			1,0,0,0,
			0,1,0,0,
			0,0,1,0,
			0,0,0,1,
			ratio, ratio, ratio, 0
		)))
	var/has_legacy_light_pref = ("light" in mymob.client.prefs.vars)
	var/enabled = TRUE
	if(has_legacy_light_pref)
		enabled = (mymob.client.prefs.light & LIGHT_GLARE)
	else
		enabled = (mymob.client.prefs.lighting_blur >= 2)
	if(enabled)
		add_filter("add_lamps_to_glare", 1, layering_filter(render_source = target_rendering, blend_mode = BLEND_ADD))
		// Комфортнее: чуть слабее radial blur
		add_filter("lamps_glare", 1, radial_blur_filter(size = 0.025))

///Contains space parallax
/atom/movable/screen/plane_master/parallax
	name = "parallax plane master"
	plane = PLANE_SPACE_PARALLAX
	blend_mode = BLEND_MULTIPLY
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	render_target = PLANE_SPACE_PARALLAX_RENDER_TARGET

/atom/movable/screen/plane_master/parallax_white
	name = "parallax backdrop/space turf plane master"
	plane = PLANE_SPACE

/atom/movable/screen/plane_master/parallax_white/Initialize(mapload)
	. = ..()
	add_filter("displacer", 3, displacement_map_filter(render_source = GRAVITY_PULSE_RENDER_TARGET, size = 10))

	add_filter("singularity_0", 1, displacement_map_filter(render_source = SINGULARITY_0_RENDER_TARGET, size = -20))
	add_filter("singularity_1", 2, displacement_map_filter(render_source = SINGULARITY_1_RENDER_TARGET, size = 75))
	add_filter("singularity_2", 3, displacement_map_filter(render_source = SINGULARITY_2_RENDER_TARGET, size = 400))
	add_filter("singularity_3", 4, displacement_map_filter(render_source = SINGULARITY_3_RENDER_TARGET, size = 700))

	animate(get_filter("singularity_0"), size = -20, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = -30, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_1"), size = 50, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 100, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_2"), size = 400, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 300, time = 10, easing = LINEAR_EASING, loop = -1)

	animate(get_filter("singularity_3"), size = 750, time = 10, easing = LINEAR_EASING, loop = -1, flags = ANIMATION_PARALLEL)
	animate(size = 600, time = 10, easing = LINEAR_EASING, loop = -1)

/atom/movable/screen/plane_master/camera_static
	name = "camera static plane master"
	plane = CAMERA_STATIC_PLANE
	appearance_flags = PLANE_MASTER
	blend_mode = BLEND_OVERLAY

//Reserved to chat messages, so they are still displayed above the field of vision masking.
/atom/movable/screen/plane_master/chat_messages
	name = "runechat plane master"
	plane = CHAT_PLANE
	appearance_flags = PLANE_MASTER
	blend_mode = BLEND_OVERLAY


/atom/movable/screen/plane_master/gravpulse
	name = "gravpulse plane"
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	plane = GRAVITY_PULSE_PLANE
	render_target = GRAVITY_PULSE_RENDER_TARGET
	blend_mode = BLEND_ADD
